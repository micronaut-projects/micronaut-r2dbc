pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
    plugins {
        id 'io.micronaut.application' version getProperty("micronautGradlePluginVersion")
        id 'io.micronaut.minimal.application' version getProperty("micronautGradlePluginVersion")
        id 'io.micronaut.library' version getProperty("micronautGradlePluginVersion")
        id "org.jetbrains.kotlin.jvm" version getProperty("kotlinVersion")
        id "org.jetbrains.kotlin.kapt" version getProperty("kotlinVersion")
        id "org.jetbrains.kotlin.plugin.allopen" version getProperty("kotlinVersion")
        id "org.jetbrains.kotlin.plugin.jpa" version getProperty("kotlinVersion")
        id 'io.micronaut.build.shared.settings' version getProperty("micronautSharedSettingVersion")
    }
}

plugins {
    id 'io.micronaut.build.shared.settings'
}

enableFeaturePreview("VERSION_CATALOGS")

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    }
}

micronautBuild {
    importMicronautCatalog()
}

rootProject.name = 'r2dbc-parent'

include 'r2dbc-bom'
include 'r2dbc-core'

// examples
include 'doc-examples:example-groovy'
include 'doc-examples:example-java'
include 'doc-examples:example-kotlin'

// tests
include 'test-graalvm'
include 'test-graalvm:postgres'
include 'test-graalvm:mysql'
include 'test-graalvm:mariadb'
if (Boolean.getBoolean('test.oracle')) {
    include 'test-graalvm:oracle'
}
include 'test-graalvm:h2'
include 'test-graalvm:mssql'
include 'test-graalvm:common'
include 'test-graalvm:common-tests'

