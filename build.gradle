buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "io.micronaut.build.internal:micronaut-gradle-plugins:4.0.0"
    }
}

subprojects { Project subproject ->

    repositories {
        mavenCentral()
        maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    }

    if (subproject.getPath().startsWith(":test-")) {
        return
    }
    group "io.micronaut.r2dbc"

    boolean isExampleOrBenchmark = subproject.parent.name == "docs-examples" || subproject.name.contains("example") || subproject.name.contains("benchmark")

    if (!isExampleOrBenchmark) {
        if (subproject.name.endsWith('-bom')) {
            apply plugin: "java-platform"
            version projectVersion
            apply plugin: "io.micronaut.build.internal.dependency-updates"
            apply plugin: "io.micronaut.build.internal.publishing"
        }  else {

            apply plugin: "io.micronaut.build.internal.common"
            apply plugin: "io.micronaut.build.internal.dependency-updates"
            apply plugin: "io.micronaut.build.internal.publishing"

            micronautBuild {
                resolutionStrategy {
                    force "jakarta.persistence:jakarta.persistence-api:$jakartaPersistenceVersion"
                    force "jakarta.transaction:jakarta.transaction-api:$jakartaTxVersion"
                }
            }
            dependencies {
                implementation 'jakarta.inject:jakarta.inject-api:2.0.0'
                testImplementation platform("org.testcontainers:testcontainers-bom:$testContainersVersion")
            }
        }
    }
}

apply plugin: "io.micronaut.build.internal.docs"
apply plugin: "io.micronaut.build.internal.dependency-updates"
