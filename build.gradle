buildscript {
    repositories {
        gradlePluginPortal()
        jcenter()
        maven { url "https://dl.bintray.com/micronaut/core-releases-local" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build:micronaut-gradle-plugins:2.0.15"
    }
}

subprojects { Project subproject ->

    repositories {
        mavenCentral()
        jcenter()
    }

    if (subproject.getPath().startsWith(":test-")) {
        return
    }
    group "io.micronaut.r2dbc"

    boolean isExampleOrBenchmark = subproject.parent.name == "docs-examples" || subproject.name.contains("example") || subproject.name.contains("benchmark")

    if (!isExampleOrBenchmark) {
        if (subproject.name.endsWith('-bom')) {
            apply plugin: "java-platform"
            version projectVersion
            apply plugin: "io.micronaut.build.dependency-updates"
            apply plugin: "io.micronaut.build.publishing"
        }  else {

            apply plugin: "io.micronaut.build.common"
            apply plugin: "io.micronaut.build.dependency-updates"
            apply plugin: "io.micronaut.build.publishing"

            micronautBuild {
                resolutionStrategy {
                    force "jakarta.persistence:jakarta.persistence-api:$jakartaPersistenceVersion"
                    force "jakarta.transaction:jakarta.transaction-api:$jakartaTxVersion"
                }
            }
            dependencies {
                testImplementation platform('org.testcontainers:testcontainers-bom:1.15.1')
            }
        }
    }
}

apply plugin: "io.micronaut.build.docs"
apply plugin: "io.micronaut.build.dependency-updates"
